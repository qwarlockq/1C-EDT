
//////////////////////////////
//		СОБЫТИЯ ФОРМЫ		//
//////////////////////////////

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Авторизация", 2);
	
КонецПроцедуры


//////////////////////////////
//	  ОСНОВНЫЕ ПРОЦЕДУРЫ	//
//////////////////////////////

&НаКлиенте
Процедура Авторизация()
	
	Если АвторизацияПройдена И Не КодВведен Тогда
		
		КодАвторизации = ПолучитьКод();		
		Если ЗначениеЗаполнено(КодАвторизации) Тогда						
			ВвестиКод(КодАвторизации);
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	ЗапросНаАвторизацию = ЕстьЗапросНаАвторизацию();
	Если ЗапросНаАвторизацию Тогда		
		
		// Сценарий авторизации (до ввода кода двухфакторной авторизации)
		Сценарий = ПредопределенноеЗначение("Справочник.СценарииВыполненияАвторизации.Авторизация");
		ПутьКФайлуСценария = СоздатьФайлСценария(Сценарий);
		
		КомандаСистемы(ПутьКФайлуСценария);
		
		ВидЗадания = ПредопределенноеЗначение("Перечисление.ВидЗаданийАвторизации.Авторизация");
		ЗаписатьВыполненноеЗадание(ВидЗадания);
		
		УдалитьФайлы(ПутьКФайлуСценария);
		
		АвторизацияПройдена = Истина;
		
		// Сценарий ввода кода
		КодАвторизации = ПолучитьКод();		
		Если ЗначениеЗаполнено(КодАвторизации) Тогда						
			ВвестиКод(КодАвторизации);			
		Иначе			
			КодВведен = Ложь;			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВключитьЭнидеск() Тогда
		ЗапуститьПриложение(ПолучитьПутьЗапускаЭнидеск());
		ЗаписатьВыполненноеЗадание(ПредопределенноеЗначение("Перечисление.ВидЗаданийАвторизации.Энидеск"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиКод(КодАвторизации)
	
	Сценарий = ПредопределенноеЗначение("Справочник.СценарииВыполненияАвторизации.ВводКода");
	ПутьКФайлуСценария = СоздатьФайлСценария(Сценарий, КодАвторизации);
	
	КомандаСистемы(ПутьКФайлуСценария);
	
	ВидЗадания = ПредопределенноеЗначение("Перечисление.ВидЗаданийАвторизации.ВводКода");
	ЗаписатьВыполненноеЗадание(ВидЗадания, КодАвторизации);
	
	УдалитьФайлы(ПутьКФайлуСценария);
	
	КодВведен 			= Истина;
	АвторизацияПройдена = Ложь;
	
КонецПроцедуры


//////////////////////////////
//   СЛУЖЕБНЫЕ ПРОЦЕДУРЫ	//
//////////////////////////////

&НаСервере
Функция ЕстьЗапросНаАвторизацию()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаданияАвторизацииСрезПоследних.Период КАК Период
		|ИЗ
		|	РегистрСведений.ЗаданияАвторизации.СрезПоследних(, ВидЗадания = ЗНАЧЕНИЕ(Перечисление.ВидЗаданийАвторизации.Авторизация)) КАК ЗаданияАвторизацииСрезПоследних
		|ГДЕ
		|	ЗаданияАвторизацииСрезПоследних.СтатусЗадания = ЗНАЧЕНИЕ(Перечисление.СтатусЗаданийАвторизации.Новое)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПолучитьКод()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаданияАвторизацииСрезПоследних.КодАвторизации КАК КодАвторизации
		|ИЗ
		|	РегистрСведений.ЗаданияАвторизации.СрезПоследних(, ВидЗадания = ЗНАЧЕНИЕ(Перечисление.ВидЗаданийАвторизации.ВводКода)) КАК ЗаданияАвторизацииСрезПоследних
		|ГДЕ
		|	ЗаданияАвторизацииСрезПоследних.СтатусЗадания = ЗНАЧЕНИЕ(Перечисление.СтатусЗаданийАвторизации.Новое)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КодАвторизации;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция СоздатьФайлСценария(Сценарий, КодАвторизации = "");
	
	Возврат Обработки.Авторизация.СоздатьФайлСценария(Сценарий, КодАвторизации);
	
КонецФункции

&НаСервере
Процедура ЗаписатьВыполненноеЗадание(ВидЗадания, КодАвторизации = "")
	
	Обработки.Авторизация.ЗаписатьЗадание(ВидЗадания, Перечисления.СтатусЗаданийАвторизации.Завершено, КодАвторизации);	
	
КонецПроцедуры

&НаСервере
Функция ВключитьЭнидеск()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаданияАвторизацииСрезПоследних.ВидЗадания
		|ИЗ
		|	РегистрСведений.ЗаданияАвторизации.СрезПоследних(,
		|		ВидЗадания = ЗНАЧЕНИЕ(Перечисление.ВидЗаданийАвторизации.Энидеск)) КАК ЗаданияАвторизацииСрезПоследних
		|ГДЕ
		|	ЗаданияАвторизацииСрезПоследних.СтатусЗадания = ЗНАЧЕНИЕ(Перечисление.СтатусЗаданийАвторизации.Новое)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ПолучитьПутьЗапускаЭнидеск()
	
	Возврат Справочники.СценарииВыполненияАвторизации.Энидеск.ТекстСценария;
	
КонецФункции
